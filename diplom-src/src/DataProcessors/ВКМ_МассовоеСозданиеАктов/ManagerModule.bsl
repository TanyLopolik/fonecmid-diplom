#Область СлужебныеПроцедурыИФункции

Функция СоздатьАктыЗаПериод(НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор КАК Договор
	|ПОМЕСТИТЬ ВТ_ДоговорыЗаПериод
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода,,) КАК
	|		ВКМ_ВыполненныеКлиентуРаботыОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Договор КАК ДоговорРеализации
	|ПОМЕСТИТЬ ВТ_ДоговорыРеализаций
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен
	|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеализацияТоваровУслуг.Договор В
	|		(ВЫБРАТЬ
	|			ВТ_ДоговорыЗаПериод.Договор
	|		ИЗ
	|			ВТ_ДоговорыЗаПериод)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДоговорыЗаПериод.Договор КАК Договор,
	|	ЕСТЬNULL(ВТ_ДоговорыРеализаций.ДоговорРеализации, 0) КАК ДоговорРеализации,
	|	ВТ_ДоговорыЗаПериод.Договор.Организация КАК Организация,
	|	ВТ_ДоговорыЗаПериод.Договор.Владелец КАК Контрагент
	|ИЗ
	|	ВТ_ДоговорыЗаПериод КАК ВТ_ДоговорыЗаПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговорыРеализаций КАК ВТ_ДоговорыРеализаций
	|		ПО (ВТ_ДоговорыЗаПериод.Договор = ВТ_ДоговорыРеализаций.ДоговорРеализации)
	|ГДЕ
	|	ЕСТЬNULL(ВТ_ДоговорыРеализаций.ДоговорРеализации, 0) = 0";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	МассивАктов = Новый Массив();
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() тогда	
		Возврат МассивАктов;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() цикл
		
		РеализацияОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Дата", КонецПериода);
		ДанныеЗаполнения.Вставить("Организация", Выборка.Организация);
		ДанныеЗаполнения.Вставить("Контрагент", Выборка.Контрагент);
		ДанныеЗаполнения.Вставить("Договор", Выборка.Договор);
		
		РеализацияОбъект.Заполнить(ДанныеЗаполнения);
		РеализацияОбъект.ВКМ_ВыполнитьАвтозаполнение();
		РеализацияОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		МассивАктов.Добавить(Новый Структура("Договор, Реализация", Выборка.Договор, РеализацияОбъект.Ссылка));
		
	КонецЦикла;
	
	Возврат МассивАктов;
	
КонецФункции

#КонецОбласти