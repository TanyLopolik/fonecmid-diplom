#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	 ДлительнаяОперация = НачатьСозданиеАктов();
	 
	 ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	 Оповещение = Новый ОписаниеОповещения("ПриЗавершенииСозданияАктов", ЭтотОбъект);
	 ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьСозданиеАктов()
	
	Объект.ТаблицаАктов.Очистить();
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
	 "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьАктыЗаПериод",
	 Объект.Период.ДатаНачала,
	 Объект.Период.ДатаОкончания);
	 
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииСозданияАктов(Результат, ДополнительныеПараметры) Экспорт

	 Если Результат = Неопределено Тогда // Пользователь отменил задание.
	  	Возврат;
	 КонецЕсли;
	 
	 Если Результат.Статус = "Ошибка" Тогда
	 	 ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	 КонецЕсли;
 
	МассивАктов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если МассивАктов.Количество() > 0 тогда
		ЗаполнитьТаблицуАктовНаСервере(МассивАктов);
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	 
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуАктовНаСервере(МассивАктов)
	
	Для каждого ЭлМассив из МассивАктов цикл
	
		НС = Объект.ТаблицаАктов.Добавить();
		ЗаполнитьЗначенияСвойств(НС, ЭлМассив);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
