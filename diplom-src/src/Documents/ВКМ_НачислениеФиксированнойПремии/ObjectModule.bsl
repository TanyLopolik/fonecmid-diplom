#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ТаблицаПремий = Неопределено;
	
	СформироватьДополнительныеНачисления(ТаблицаПремий);
	СформироватьУдержаниеНДФЛ(ТаблицаПремий);
	СформироватьВзаиморасчетыССотрудником(ТаблицаПремий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДополнительныеНачисления(ТаблицаПремий)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СУММА(ВКМ_НачислениеФиксированнойПремииСписокСотрудников.СуммаПремии) КАК СуммаПремии,
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник
	|ИЗ
	|	Документ.ВКМ_НачислениеФиксированнойПремии.СписокСотрудников КАК ВКМ_НачислениеФиксированнойПремииСписокСотрудников
	|ГДЕ
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ТаблицаПремий = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрСотрудник из ТаблицаПремий цикл
	
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = СтрСотрудник.Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПремияФиксированная;
		Движение.Результат = СтрСотрудник.СуммаПремии;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура СформироватьУдержаниеНДФЛ(ТаблицаПремий)

	
	Для каждого СтрСотрудник из ТаблицаПремий цикл
	
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = СтрСотрудник.Сотрудник;
		Движение.Результат = (СтрСотрудник.СуммаПремии*13)/100;	
		
		СтрСотрудник.СуммаПремии = СтрСотрудник.СуммаПремии - Движение.Результат;
		
	КонецЦикла;	
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура СформироватьВзаиморасчетыССотрудником(ТаблицаПремий)
	
	Для каждого СтрСотрудник из ТаблицаПремий цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудником.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Сотрудник = СтрСотрудник.Сотрудник;
		Движение.Сумма = СтрСотрудник.СуммаПремии;
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудником.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли