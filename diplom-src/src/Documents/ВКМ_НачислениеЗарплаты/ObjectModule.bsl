#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения(Отказ);
	РассчитатьОклад();
	РассчитатьОтпуск();
	РассчитатьНДФЛ();
	СформироватьВзаиморасчетыССотрудниками();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаНачала,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаОкончания,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ГрафикРаботы,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Сотрудник
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.ОсновныеНачисления КАК ВКМ_НачислениеЗарплатыОсновныеНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.ДатаНачала,
	|	ВТ_ДанныеДокумента.ДатаОкончания,
	|	ВТ_ДанныеДокумента.ГрафикРаботы,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.Сотрудник.Представление КАК ПредставлениеСотрудника,
	|	ЕстьNull(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Показатель
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТ_ДанныеДокумента.Сотрудник
	|			ИЗ
	|				ВТ_ДанныеДокумента)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВТ_ДанныеДокумента.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Дата));
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() цикл
		
		Если Результат.Показатель = 0 тогда
			Отказ = Истина;
			ТекстСообщения = СтрШаблон("Для сотрудника %1 не задано значение оклада", Результат.ПредставлениеСотрудника);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);	
			Продолжить;
		КонецЕсли;
					
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = Результат.ВидРасчета;
		Движение.ПериодДействияНачало = Результат.ДатаНачала;
		Движение.ПериодДействияКонец = Результат.ДатаОкончания;
		Движение.Сотрудник = Результат.Сотрудник;
		Движение.ГрафикРаботы = Результат.ГрафикРаботы;
		Если Результат.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад тогда
			Движение.Показатель = Результат.Показатель;
		Иначе
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Результат.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Результат.ДатаОкончания, -1));
		КонецЕсли;
	
		
	КонецЦикла;
	
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаНачисление ИЗ ДополнительныеНачисления Цикл		
	
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = СтрокаНачисление.ВидРасчета;
		Движение.Сотрудник = СтрокаНачисление.Сотрудник;
		Движение.Результат = СтрокаНачисление.СуммаНачисления;	
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК План,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &Оклад
	|	И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";   
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		Движение.Результат = Движение.Показатель * Выборка.Факт / Выборка.План; 
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК База,
	|	РАЗНОСТЬДАТ(ОсновныеНачисленияБазаОсновныеНачисления.ПериодДействияНачало,
	|		ОсновныеНачисленияБазаОсновныеНачисления.ПериодДействияКонец, ДЕНЬ) + 1 КАК ФактДней,
	|	ОсновныеНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ОсновныеНачисленияБазаОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ОсновныеНачисленияБазаОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец
	|ПОМЕСТИТЬ ВТ_ДанныеДляРасчета
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,, Регистратор = &Ссылка
	|	И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияБазаОсновныеНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия / 8) КАК
	|		ЗначениеФактическийПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_ФактВремя
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Сотрудник В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ВТ_ДанныеДляРасчета.Сотрудник
	|		ИЗ
	|			ВТ_ДанныеДляРасчета)
	|	И ВидРасчета = &Оклад
	|	И (ПериодРегистрации МЕЖДУ &ДатаС И &ДатаПо)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДляРасчета.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеДляРасчета.База КАК База,
	|	ВТ_ДанныеДляРасчета.ФактДней КАК ФактДнейОтпуска,
	|	ВТ_ФактВремя.ЗначениеФактическийПериодДействия КАК ФактОтработано
	|ИЗ
	|	ВТ_ДанныеДляРасчета КАК ВТ_ДанныеДляРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФактВремя КАК ВТ_ФактВремя
	|		ПО ВТ_ФактВремя.Сотрудник = ВТ_ДанныеДляРасчета.Сотрудник";
	
	Измерения = Новый Массив;				
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения",Измерения); 
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаС", НачалоМесяца(ДобавитьМесяц(Дата, -12)));
	Запрос.УстановитьПараметр("ДатаПо", КонецМесяца(ДобавитьМесяц(Дата, -1)));

	ТЗВыборка = Запрос.Выполнить().Выгрузить();	
	
	Для каждого Выборка ИЗ ТЗВыборка цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		Движение.Результат = Выборка.База / Выборка.ФактОтработано * Выборка.ФактДнейОтпуска; 
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.Сотрудник,
	|	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК Результат
	|ПОМЕСТИТЬ ВТ_ДанныеДляРасчета
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисления.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.Сотрудник,
	|	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК Результат
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ДополнительныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДляРасчета.Сотрудник,
	|	СУММА(ВТ_ДанныеДляРасчета.Результат) КАК Результат
	|ИЗ
	|	ВТ_ДанныеДляРасчета КАК ВТ_ДанныеДляРасчета
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДляРасчета.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Результат = (Выборка.Результат * 13) /100;
			
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура СформироватьВзаиморасчетыССотрудниками()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.Сотрудник,
	|	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК Результат
	|ПОМЕСТИТЬ ВТ_ДанныеДляРасчета
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисления.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.Сотрудник,
	|	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК Результат
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ДополнительныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДляРасчета.Сотрудник,
	|	СУММА(ВТ_ДанныеДляРасчета.Результат) КАК Результат,
	|	СУММА(ВКМ_Удержания.Результат) КАК Удержание
	|ИЗ
	|	ВТ_ДанныеДляРасчета КАК ВТ_ДанныеДляРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|		ПО ВТ_ДанныеДляРасчета.Сотрудник = ВКМ_Удержания.Сотрудник
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДляРасчета.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудником.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Результат - Выборка.Удержание;
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудником.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли