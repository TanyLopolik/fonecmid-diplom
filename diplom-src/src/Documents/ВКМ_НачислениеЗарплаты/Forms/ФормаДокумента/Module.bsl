#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	НачалоМесяца = НачалоМесяца(Объект.Дата);
	КонецМесяца = КонецМесяца(Объект.Дата);
	
	Для каждого СтрНачисление из Объект.ОсновныеНачисления цикл
		
		Если СтрНачисление.ДатаНачала < НачалоМесяца ИЛИ СтрНачисление.ДатаОкончания > КонецМесяца тогда
			Отказ = Истина;
			ТекстСообщения = СтрШаблон("В строке %1 интервал дат не принадлежит месяцу документа", 
			СтрНачисление.НомерСтроки);
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область  ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.ОсновныеНачисления.Очистить();
	Объект.ДополнительныеНачисления.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник,
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот
	|ПОМЕСТИТЬ ВТ_ВыполненныеРаботы
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&ДатаНачала, &ДатаКонца,,) КАК
	|		ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ФизическиеЛица.Ссылка КАК Сотрудник,
	|	ЕстьNuLL(ВТ_ВыполненныеРаботы.СуммаКОплатеОборот, 0) КАК СуммаКОплате
	|ИЗ
	|	Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыполненныеРаботы КАК ВТ_ВыполненныеРаботы
	|		ПО ВТ_ВыполненныеРаботы.Сотрудник = ВКМ_ФизическиеЛица.Ссылка
	|	ГДЕ НЕ ВКМ_ФизическиеЛица.ЭтоГруппа И НЕ ВКМ_ФизическиеЛица.ПометкаУдаления";	
	
	НачалоМесяца = НачалоМесяца(Объект.Дата);
	КонецМесяца = КонецМесяца(Объект.Дата);
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца);
	Запрос.УстановитьПараметр("ДатаКонца", КонецМесяца);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() цикл
		
		НоваяСтрокаОН = Объект.ОсновныеНачисления.Добавить();
		НоваяСтрокаОН.Сотрудник = Результат.Сотрудник;
		НоваяСтрокаОН.ДатаНачала = НачалоМесяца;
		НоваяСтрокаОН.ДатаОкончания = КонецМесяца;
	
		Если Результат.СуммаКОплате = 0 тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаДН = Объект.ДополнительныеНачисления.Добавить();
		НоваяСтрокаДН.Сотрудник = Результат.Сотрудник;
		НоваяСтрокаДН.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		НоваяСтрокаДН.СуммаНачисления = Результат.СуммаКОплате;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти