
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//ВКМ - команда печати
	// Акт об оказании услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВКМ_Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Порядок = 5;
	//--

КонецПроцедуры

#КонецОбласти

//ВКМ
#Область СлужебныеПроцедурыИФункции
//--

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ВКМ_Акт");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ВКМ_ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт выполненных работ'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_Акт";
	КонецЕсли;
		
	
КонецПроцедуры

Функция ВКМ_ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктОбОказанииУслуг";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_Акт");
	
	ДанныеДокументов = ВКМ_ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВКМ_ВывестиШапкуРеализации(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);

		ВКМ_ВывестиСтрокиУслуги(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВКМ_ВывестиПодвалРеализации(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ВКМ_ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Номер,
	|	Представление(РеализацияТоваровУслуг.Организация) КАК Организация,
	|	РеализацияТоваровУслуг.Организация.ИНН КАК ОрганизацияИНН,
	|	Представление(РеализацияТоваровУслуг.Контрагент) КАК Контрагент,
	|	РеализацияТоваровУслуг.Контрагент.ИНН КАК КонтрагентИНН,
	|	Представление(РеализацияТоваровУслуг.Договор) КАК Договор,
	|	РеализацияТоваровУслуг.Услуги.(
	|		Представление(Номенклатура) КАК Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма,
	|		Представление(Номенклатура.ЕдиницаИзмерения) КАК Единица),
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	ЕСТЬNULL(РеализацияТовары.Сумма, 0) КАК СуммаТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СУММА(РеализацияТоваровУслугТовары.Сумма) КАК Сумма,
	|			РеализацияТоваровУслугТовары.Ссылка
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ГДЕ
	|			РеализацияТоваровУслугТовары.Ссылка В (&МассивОбъектов)
	|		СГРУППИРОВАТЬ ПО
	|			РеализацияТоваровУслугТовары.Ссылка) КАК РеализацияТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТовары.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВКМ_ВывестиШапкуРеализации(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Шапка");
	
	ДанныеПечати = Новый Структура;
	
	ДанныеПечати.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
	ДанныеПечати.Вставить("Дата", Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ОписаниеОрганизации", СтрШаблон("%1, ИНН %2", 
	ДанныеДокументов.Организация, 
	ДанныеДокументов.ОрганизацияИНН));
	
	ДанныеПечати.Вставить("ОписаниеКонтрагента", СтрШаблон("%1, ИНН %2", 
	ДанныеДокументов.Контрагент, 
	ДанныеДокументов.КонтрагентИНН));
	
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры

Процедура ВКМ_ВывестиСтрокиУслуги(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьСтрокаУслуги = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();

	Пока ВыборкаУслуги.Следующий() цикл
		
		ОбластьСтрокаУслуги.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьСтрокаУслуги);	

		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВКМ_ВывестиПодвалРеализации(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ИтогоСумма = ДанныеДокументов.СуммаДокумента - ДанныеДокументов.СуммаТоваров;
	ОбластьПодвал.Параметры.ИтогоСумма = Формат(ИтогоСумма, "ЧДЦ=2");
	ОбластьПодвал.Параметры.ИтогоСуммаСтрокой = СтрШаблон("%1 (%2)", 
	Формат(ИтогоСумма, "ЧДЦ=2"), 
	ЧислоПрописью(ИтогоСумма, ,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж"));
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры

//ВКМ
#КонецОбласти
//--

#КонецЕсли